apply plugin: 'com.android.library'
apply from: '../config/quality/quality.gradle'
apply from: '../config/publish.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable "GradleDependency", "OldTargetApi"
    }

    testOptions {
        unitTests.all {
            systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
        }
    }
}

task internalJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    destinationDir = file("../docs/internal")
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

task releaseJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    destinationDir = file("../docs/public")
    failOnError false
}

build.doFirst {
    generateVersionFile
}

task generateVersionFile {
    def folder = file('src/main/java/com/vibes/vibes')
    def srcFile = new File(folder, "VibesBuild.java")
    srcFile.createNewFile()
    srcFile.text = """package com.vibes.vibes;
public class VibesBuild {
    public static final String SDK_VERSION = "$ARTIFACT_VERSION";
}
"""
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'junit:junit:4.12'
    testImplementation('org.robolectric:robolectric:3.8') {
        // Conflicting Bouncycastle version in Robolectric and MockWebServer
        // Related issue
        // https://github.com/square/okhttp/issues/3672
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    testImplementation 'org.mockito:mockito-core:3.0.0'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
